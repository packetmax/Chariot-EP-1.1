## Synopsis
The purpose of this sketch is to provide a basic websocket server that websocket clients can
use to gain access to any Chariot in the 6LoWPAN mesh of which this Chariot-equipped server is a member. This sketch uses the Mega 2560 and W5100-compatible Ethernet shield with **Chariot** as a relay--directing CoaP URLs received from a client across an ethernet-hosted websocket to the sketch. The sketch inspects each and forwards the ones not destined for local processing to the proxy located on the local Chariot which then takes care of forwarding each request to the intended target and delivering the response for relay back to the requestor. The websocket relay server created by this sketch can be confined to a local network or alternatively given a dynamic DNS address for access from the net.
 
A javascript client that can be opened and run by Chrome is provided to act as a test and prototyping fixture.  

## Sketch code details
This sketch uses the websockets library  by Per Ejeklint for Arduino. See: 
<https://github.com/ejeklint/ArduinoWebsocketServer>. During setup, the **Chariot** serial channel is created, followed by the registration of *connection*, *data*, and *disconnect* 
callbacks with the websocket server.
### Ethernet/Websocket server setup ###
```c++
 //---Put Arduino resources on the air---
  ChariotEP.begin();
  
  //---Put Ethernet on the air (so to speak)---
  Ethernet.begin(mac, ip);
  delay(1000); // Give Ethernet delay to get initialized
  
  wsServer.registerConnectCallback(&onConnect);
  wsServer.registerDataCallback(&onData);
  wsServer.registerDisconnectCallback(&onDisconnect);  
  wsServer.begin();
```
### Incoming websocket connects ###
When a websocket client attempts to connect to the server an event is generated causing the callback that was registered for such occasions to be executed. The callback function is invoked with the new socket handle as its argument. This socket will be saved in a connection table whose size (in slots) is configurable by way of a #define. If room in the table or the available cache of sockets has been exhausted by previous connections, a message is displayed on the serial port and the request refused.

```c++
#include <WebSocket.h>

// Create a Websocket server and sockets
WebSocketServer wsServer;
#define MAX_WEB_SOCKETS (4) // See <websocket.h>
WebSocket *openSockets[MAX_WEB_SOCKETS]  = {NULL};

void onConnect(WebSocket &socket) {
  WebSocket *ws_p = (WebSocket *)&socket;
  int i;
  for (i = 0; i < MAX_WEB_SOCKETS-1; i++) {
    if (ws_p == openSockets[i]) {
      SerialMon.println(F("ERROR! Websocket connection cannot be opened--no available sockets."));
      return; // we already have this one cached (ERR)
    }
  }
  
  // Plug socket into first open slot in connection table.
  for (i = 0; i < MAX_WEB_SOCKETS-1; i++) {
    if (openSockets[i] == NULL) {
      openSockets[i] = ws_p;
      SerialMon.print(F("Websocket connection opened = "));
      SerialMon.println(i);
      return;
    }
  }
  SerialMon.println(F("ERROR! Websocket connection cannot be opened--no available slots."));
}
```
### Incoming websocket data ###
This is the *data arrival callback* invoked by the server when a data frame is received. This sketch limits the incoming message length to 125 bytes, *or one data frame*, although it is possible to extend this concept to larger data packets. Additionally, the ethernet and websocket libraries are constrained to delivering simple ascii text only. The latter constraint is due to Arduino's space constraints.

There are several processing steps once the command has arrived completely. First, the incoming *String* is trimmed to remove trailing spaces. It is also nul-terminated and set to lower case. Next, if the head of the string is either "coap" or "chariot" the arrival is a URI or an abbreviated Chariot command (type 'help' into the Serial window of any of our sketches to see what they consist of). If it was the former, the URI/command is sent to Chariot and processing is completed. If the command begins with "arduino", then this is a request from the websocket to get or set a pin value, set the mode of a pin (INPUT, OUTPUT, INPUT_PULLUP), etc. In that case processing occurs and the resulting status returned to the requestor.

**** Note ****
This sketch is an example of an websocket relay only. It has been kept as simple as possible to provide an exemplar of this important function. In order to keep things as simple and short as possible, the ability to respond to Arduino pin URI's generated by other sketches has not been placed in this sketch. That capability is *core" to **Chariot** and is included in the other sketches. **Chariot** will respond to all RESTful requests of its resources (temp, accelerometer, magnetometer, soft resources, etc), made by other motes on the mesh.
```c++
void onData(WebSocket &socket, char* dataString, byte frameLength) 
{
  /*
   * Candidate URL or command received over the websocket server.
   */
  String chariotUrlOrCmd = String(dataString);
     
  /**
   * 1. Cleanup: end of chariotUrlOrCmd line input reached--trim whitespace and nul terminate
   */
  chariotUrlOrCmd.trim();
  if (chariotUrlOrCmd.length() >= MAX_CHARIOT_CMD_LEN) {
    chariotUrlOrCmd.setCharAt(MAX_CHARIOT_CMD_LEN, '\0');
  }
  
  // Simplify all comparisons, make all input lower case.
  chariotUrlOrCmd.toLowerCase();
  
 /**
  * 2. Process coap:// and coap:// URI's here
  */
  if (chariotUrlOrCmd.indexOf(F("coap")) != -1) {
    chariotUrlOrCmd += "\n\0";    // terminate for Chariot
    ChariotClient.print(chariotUrlOrCmd);
    coapResponse(socket);
  }
    
 /**
  * 3. Process local Chariot command URI's here
  */
  else if (chariotUrlOrCmd.indexOf(F("chariot")) != -1) {
    chariotUrlOrCmd.remove(0, 8); // remove "chariot/"
    chariotUrlOrCmd += "\n\0";    // terminate for Chariot
    ChariotClient.print(chariotUrlOrCmd);
    coapResponse(socket);
  }

  /**
   * 4. Process local arduino firmata cmds here
   */
  else if (chariotUrlOrCmd.indexOf(F("arduino/"))!= -1) {
     chariotUrlOrCmd.remove(0, 8);
     processArduinoCmd(socket, chariotUrlOrCmd);
  }
}
```
### Websocket client disconnect ###
This simply looks up the socket in the connection table and *forgets* it.
```c++
void onDisconnect(WebSocket &socket) {
  // Find and remove socket in connection table.
  WebSocket *ws_p = &socket;
  int i;
  for (i = 0; i < MAX_WEB_SOCKETS-1; i++) {
    if (openSockets[i] == ws_p) {
      openSockets[i] = NULL;
      SerialMon.print(F("Websocket connection closed = "));
      SerialMon.println(i);
      return;
    }
  }
}
```
### Other code sections ###
In our opinion, there is quite alot going on with this server, considering this is an Arduino sketch. In particular, the arduino pin processing shown here is pretty involved and worth taking a look at for the interested hacker. The *loop()* code is also worth making note of.

# Additional requirements and considerations
- The **Serial Monitor**, when used, should be set to *9600 baud* and *Newline* line termination set.
- Chariot (ie., **ChariotClient**) communicates with the *Mega 2560* on **Serial3** port.
- This sketch runs on the Mega 2560, or any of the current knock-offs, which are available on Amazon. 
- This sketch will **NOT** also run on the *UNO*, which has too little flash to hold the code.
- The Websocket library for Arduino and Ethernet libraries are included by the sketch (see **Synopsis)*.
- Due the fact that the inexpensive W5100-compatible ethernet shields are not R3-connector compatible, they are missing two pins on each side: SCL and SDA on the upper digital connector and IOREF (the 2nd pin is a no-connect). These must be jumpered around the ethernet shield since it is the middle board in the stackup.
- The websocket library is included in the examples folder with the sketch. It must be moved to your libraries folder.


> Qualia Networks Incorporated -- Chariot IoT Shield and software for Arduino              
> Copyright 2016, Qualia Networks, Inc.
